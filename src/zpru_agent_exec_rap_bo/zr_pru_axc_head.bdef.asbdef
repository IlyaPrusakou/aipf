managed implementation in class ZBP_R_PRU_AXC_HEAD unique;
strict ( 2 );
with draft;
extensible;
define behavior for ZR_PRU_AXC_HEAD alias executionHeader
persistent table zpru_axc_head
extensible
draft table zpru_axc_head_d
etag master AIPF7LocalLastChanged
lock master total etag AIPF7LastChanged
authorization master ( global )
{
  field ( numbering : managed ) AIPF7RunUUID;

  field ( readonly )
  AIPF7RunUUID,
  AIPF7LastChanged,
  AIPF7LocalLastChanged;

  create;
  update;
  delete;

  association _executionquery { create; with draft; }
  association _executionstep { with draft; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zpru_axc_head corresponding extensible
    {
      AIPF7RunUUID          = runuuid;
      AIPF7RunID            = runid;
      AIPF7AgentUUID        = agentuuid;
      AIPF7UserID           = userid;
      AIPF7RunStartDateTime   = starttimestamp;
      AIPF7RunEndDateTime     = endtimestamp;
      AIPF7CreatedBy        = createdby;
      AIPF7CreatedAt        = createdat;
      AIPF7ChangedBy        = changedby;
      AIPF7LastChanged      = lastchanged;
      AIPF7LocalLastChanged = locallastchanged;
    }

}

define behavior for ZR_PRU_AXC_QUERY alias executionQuery
persistent table zpru_axc_query
extensible
draft table zpru_axc_query_d
etag dependent by _executionheader
lock dependent by _executionheader
authorization dependent by _executionheader

{

  field ( numbering : managed ) AIPF7QueryUuid;
  field ( readonly ) AIPF7QueryUuid, AIPF7RunUuid;

  update;
  delete;

  association _executionheader { with draft; }
  association _executionstep { create; with draft; }

  mapping for zpru_axc_query corresponding extensible
    {
      AIPF7QueryUuid       = queryuuid;
      AIPF7QueryNumber     = querynumber;
      AIPF7RunUuid         = runuuid;
      AIPF7QueryLanguage        = language;
      AIPF7QueryStatus = executionstatus;
      AIPF7QueryStartDateTime  = starttimestamp;
      AIPF7QueryEndDateTime    = endtimestamp;
      AIPF7QueryInputPrompt     = inputprompt;
      AIPF7QueryDecisionLog     = decisionlog;
      AIPF7QueryOutputResponse  = outputresponse;
    }

}

define behavior for ZR_PRU_AXC_STEP alias executionStep
persistent table zpru_axc_step
extensible
draft table zpru_axc_step_d
etag dependent by _executionheader
lock dependent by _executionheader
authorization dependent by _executionheader

{

  field ( numbering : managed ) AIPF7StepUuid;
  field ( readonly ) AIPF7StepUuid, AIPF7QueryUuid, AIPF7RunUuid;

  update;
  delete;

  association _executionquery { with draft; }
  association _executionheader { with draft; }

  mapping for zpru_axc_step corresponding extensible
    {
      AIPF7StepUuid       = stepuuid;
      AIPF7StepNumber     = stepnumber;
      AIPF7QueryUuid      = queryuuid;
      AIPF7RunUuid        = runuuid;
      AIPF7ToolUuid       = tooluuid;
      AIPF7StepSequence   = executionseq;
      AIPF7StepStatus     = stepstatus;
      AIPF7StepStartDateTime = starttimestamp;
      AIPF7StepEndDateTime   = endtimestamp;
      AIPF7StepInputPrompt    = inputprompt;
      AIPF7StepOutputResponse   = outputprompt;
    }


}