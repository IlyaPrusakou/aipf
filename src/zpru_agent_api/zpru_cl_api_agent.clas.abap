CLASS zpru_cl_api_agent DEFINITION
  PUBLIC FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.
    INTERFACES zpru_if_agent_frw.
    INTERFACES zpru_if_api_agent.

  PROTECTED SECTION.
    DATA mo_controller       TYPE REF TO zpru_if_agent_controller.
    DATA mo_short_memory     TYPE REF TO zpru_if_short_memory_provider.

    DATA ms_agent            TYPE zpru_agent.
    DATA mt_agent_tools      TYPE STANDARD TABLE OF zpru_agent_tool WITH EMPTY KEY.
    DATA mv_input_query      TYPE zpru_if_agent_frw=>ts_json.

    DATA ms_execution_header TYPE zpru_axc_head.
    DATA ms_execution_query  TYPE zpru_axc_query.
    DATA mt_execution_steps  TYPE STANDARD TABLE OF zpru_axc_step WITH EMPTY KEY.

    METHODS get_short_memory
      RETURNING VALUE(ro_short_memory) TYPE REF TO zpru_if_short_memory_provider.

ENDCLASS.


CLASS zpru_cl_api_agent IMPLEMENTATION.
  METHOD zpru_if_api_agent~save_execution.
    DATA lo_axc_database_access TYPE REF TO zpru_if_axc_database_access.

    IF ms_execution_header IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    lo_axc_database_access = NEW zpru_cl_axc_database_access( ).

    lo_axc_database_access->modify_head( EXPORTING it_axc_head = VALUE #( ( ms_execution_header ) )
                                         IMPORTING ev_error    = DATA(lv_head_error) ).
    IF lv_head_error = abap_true.
      IF iv_do_commit = abap_true.
        ROLLBACK WORK.
      ENDIF.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
    ENDIF.

    lo_axc_database_access->modify_query( EXPORTING it_axc_query = VALUE #( ( ms_execution_query ) )
                                          IMPORTING ev_error     = DATA(lv_query_error) ).
    IF lv_query_error = abap_true.
      IF iv_do_commit = abap_true.
        ROLLBACK WORK.
      ENDIF.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
    ENDIF.

    lo_axc_database_access->modify_step( EXPORTING it_axc_step = mt_execution_steps
                                         IMPORTING ev_error    = DATA(lv_step_error) ).
    IF lv_step_error = abap_true.
      IF iv_do_commit = abap_true.
        ROLLBACK WORK.
      ENDIF.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
    ENDIF.

    IF iv_do_commit = abap_true.
      COMMIT WORK AND WAIT.
    ENDIF.
  ENDMETHOD.

  METHOD zpru_if_api_agent~build_execution.
    DATA lo_decision_provider      TYPE REF TO zpru_if_decision_provider.
    DATA lo_query                  TYPE REF TO zpru_if_request.
    DATA lt_execution_plan         TYPE zpru_if_decision_provider=>tt_execution_plan.
    DATA lo_short_memory           TYPE REF TO zpru_if_short_memory_provider.
    DATA lo_long_memory            TYPE REF TO zpru_if_long_memory_provider.
    DATA lo_agent_info_provider    TYPE REF TO zpru_if_agent_info_provider.
    DATA lo_system_prompt_provider TYPE REF TO zpru_if_prompt_provider.
    DATA lo_first_tool_input       TYPE REF TO zpru_if_response.

    IF    ms_agent       IS INITIAL
       OR mv_input_query IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    CREATE OBJECT lo_decision_provider TYPE (ms_agent-decision_provider).

    lo_short_memory = get_short_memory( ).

    IF ms_agent-long_memory_provider IS NOT INITIAL.
      CREATE OBJECT lo_long_memory TYPE (ms_agent-long_memory_provider).
    ENDIF.

    IF ms_agent-agent_info_provider IS NOT INITIAL.
      CREATE OBJECT lo_agent_info_provider TYPE (ms_agent-agent_info_provider).
    ENDIF.

    IF ms_agent-system_prompt_provider IS NOT INITIAL.
      CREATE OBJECT lo_system_prompt_provider TYPE (ms_agent-system_prompt_provider).
    ENDIF.

    lo_query = NEW zpru_cl_request( ).
    lo_query->set_data( ir_data = NEW zpru_if_agent_frw=>ts_json( mv_input_query ) ).

    mo_controller->mv_agent_uuid = ms_agent-agent_uuid.

    lo_decision_provider->call_decision_engine( EXPORTING io_controller          = mo_controller
                                                          io_input               = lo_query
                                                          io_system_prompt       = lo_system_prompt_provider
                                                          io_short_memory        = lo_short_memory
                                                          io_long_memory         = lo_long_memory
                                                          io_agent_info_provider = lo_agent_info_provider
                                                IMPORTING et_execution_plan      = lt_execution_plan
                                                          eo_first_tool_input    = lo_first_tool_input ).

    IF lt_execution_plan IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    " create execution header
    GET TIME STAMP FIELD DATA(lv_now).

    TRY.
        ms_execution_header-run_uuid        = cl_system_uuid=>create_uuid_x16_static( ).
        ms_execution_header-agent_uuid      = ms_agent-agent_uuid.
        ms_execution_header-user_id         = sy-uname.
        ms_execution_header-start_timestamp = lv_now.
*        ms_execution_header-end_timestamp        =  .
        ms_execution_header-run_status      = 'N'.

        DATA(lt_history) = lo_short_memory->get_history( ).

        ms_execution_query-query_uuid   = cl_system_uuid=>create_uuid_x16_static( ).
        ms_execution_query-run_uuid     = ms_execution_header-run_uuid.
        ms_execution_query-language     = sy-langu. " where to take?
*        ms_execution_query-input_prompt = VALUE #( lt_history[ 1 ]- OPTIONAL ). " only user input or full first prompt?
*ms_execution_query-output_response     =  .

        LOOP AT lt_execution_plan ASSIGNING FIELD-SYMBOL(<ls_tool>).

          ASSIGN mt_agent_tools[ agent_uuid = <ls_tool>-agent_uuid
                                 tool_name  = <ls_tool>-tool_name ] TO FIELD-SYMBOL(<ls_tool_master_data>).
          IF sy-subrc <> 0.
            RETURN.
          ENDIF.

          APPEND INITIAL LINE TO mt_execution_steps ASSIGNING FIELD-SYMBOL(<ls_execution_step>).
          <ls_execution_step>-step_uuid       = cl_system_uuid=>create_uuid_x16_static( ).
          <ls_execution_step>-query_uuid      = ms_execution_query-query_uuid.
          <ls_execution_step>-run_uuid        = ms_execution_header-run_uuid.
          <ls_execution_step>-tool_uuid       = <ls_tool_master_data>-tool_uuid.
          <ls_execution_step>-execution_seq   = <ls_tool>-sequence.
          <ls_execution_step>-start_timestamp = lv_now.
*          <ls_execution_step>-end_timestamp       =  .
          IF lo_first_tool_input IS BOUND.
            <ls_execution_step>-input_prompt = lo_first_tool_input->get_data( )->*.
          ENDIF.
*          <ls_execution_step>-output_prompt       =  .
        ENDLOOP.

      CATCH cx_uuid_error.
        RETURN.
    ENDTRY.
  ENDMETHOD.

  METHOD zpru_if_api_agent~initialize.
    DATA lo_adf_database_access TYPE REF TO zpru_if_adf_database_access.
    DATA lo_short_memory        TYPE REF TO zpru_if_short_memory_provider.
    DATA lt_message TYPE zpru_tt_key_value_tuple.

    IF iv_agent_name IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    mo_controller = NEW zpru_cl_agent_controller( ).

    lo_adf_database_access = NEW zpru_cl_adf_database_access( ).

    lo_adf_database_access->query_agent( EXPORTING it_agent_name      = VALUE #( ( sign   = `I`
                                                                                   option = `EQ`
                                                                                   low    = iv_agent_name ) )
                                         IMPORTING et_agent_k         = DATA(lt_agent_k)
                                                   et_tool_agent_link = DATA(et_tool_agent_link) ).

    DATA(lt_agent) = lo_adf_database_access->select_agent( it_agent_k = lt_agent_k ).

    IF lt_agent IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    ms_agent = VALUE #( lt_agent[ 1 ] OPTIONAL ).

    IF ms_agent IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    IF et_tool_agent_link IS INITIAL.
      RETURN.
    ENDIF.

    mt_agent_tools = lo_adf_database_access->select_agent_tool(
                         it_agent_tool_k = VALUE #( FOR <ls_l> IN et_tool_agent_link
                                                    WHERE ( agent_uuid = ms_agent-agent_uuid )
                                                    ( tool_uuid = <ls_l>-tool_uuid ) ) ).

    lo_short_memory = get_short_memory( ).

    lt_message = VALUE #( ( name = 'STAGE'
                            value = 'INITIALIZE' )
                          ( name = 'AGENT_NAME'
                            value = iv_agent_name ) ).

    LOOP AT mt_agent_tools ASSIGNING FIELD-SYMBOL(<ls_tool>).
      APPEND INITIAL LINE TO lt_message ASSIGNING FIELD-SYMBOL(<ls_message>).
      <ls_message>-name = 'TOOL'.
      <ls_message>-value = <ls_tool>-tool_name.
    ENDLOOP.


    lo_short_memory->save_message(
      is_message = VALUE #( agent_uuid   = ms_agent-agent_uuid
                            message_type = zpru_if_short_memory_provider=>info
                            message_tuple = lt_message ) ).
  ENDMETHOD.

  METHOD zpru_if_api_agent~rerun_execution.
  ENDMETHOD.

  METHOD zpru_if_api_agent~rerun_from_step.
  ENDMETHOD.

  METHOD zpru_if_api_agent~run.
    DATA lo_executor TYPE REF TO zpru_if_tool_executor.
    DATA lo_input    TYPE REF TO zpru_if_request.
    DATA lo_output   TYPE REF TO zpru_if_response.

    IF    ms_execution_header IS INITIAL
       OR ms_execution_query  IS INITIAL
       OR mt_execution_steps  IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    LOOP AT mt_execution_steps ASSIGNING FIELD-SYMBOL(<ls_execution_step>).

      DATA(lv_tabix) = sy-tabix.

      ASSIGN mt_agent_tools[ tool_uuid = <ls_execution_step>-tool_uuid ] TO FIELD-SYMBOL(<ls_tool_master_data>).
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

      CREATE OBJECT lo_executor TYPE (<ls_tool_master_data>-tool_provider).
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

      IF lv_tabix = 1.
        lo_input = NEW zpru_cl_request( ).
        lo_input->set_data( ir_data = REF #( <ls_execution_step>-input_prompt ) ).
        lo_output = NEW zpru_cl_response( ).
      ELSE.
        DATA(lr_output) = lo_output->get_data( ).
        IF lr_output IS BOUND AND lr_output->* IS NOT INITIAL.
          lo_input->set_data( ir_data = lo_output->get_data( ) ).
        ELSE.
          lo_input->clear_data( ).
        ENDIF.
        lo_output->clear_data( ).
      ENDIF.

      lo_executor->execute_tool( EXPORTING io_controller = mo_controller
                                           io_request    = lo_input
                                 IMPORTING eo_response   = lo_output ).

      IF mo_controller->mv_stop_agent = abap_true.
        EXIT.
      ENDIF.

    ENDLOOP.
  ENDMETHOD.

  METHOD zpru_if_api_agent~set_input_query.
    DATA lo_short_memory TYPE REF TO zpru_if_short_memory_provider.

    IF iv_input_query IS INITIAL.
      RAISE EXCEPTION NEW zpru_cx_agent_core( ).
      RETURN.
    ENDIF.

    lo_short_memory = get_short_memory( ).
*    lo_short_memory->save_message( is_message   = iv_input_query ).

    mv_input_query = iv_input_query.
  ENDMETHOD.

  METHOD get_short_memory.
    IF mo_short_memory IS BOUND.
      ro_short_memory = mo_short_memory.
      RETURN.
    ENDIF.

    IF ms_agent-short_memory_provider IS NOT INITIAL.
      CREATE OBJECT mo_short_memory TYPE (ms_agent-short_memory_provider).
    ENDIF.
  ENDMETHOD.
ENDCLASS.
